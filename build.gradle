plugins {
    id "java"
    id "com.qixalite.spongestart" version "1.6.0"
    id "com.github.johnrengelman.shadow" version "1.2.3"
}

defaultTasks "build"

group = "com.nguyenquyhy"
version = "${major}.${minor}.${patch}-${api}"
description = "A plugin to connect Discord and Minecraft"

compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

repositories {
    mavenCentral()
    maven {
        name "Sonatype"
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
    maven {
        name "sponge"
        url "http://repo.spongepowered.org/maven"
    }
    maven {
        name "javacord-repo"
        url "http://repo.bastian-oppermann.de"
    }
    maven {
        name "jitpack.io"
        url "https://jitpack.io"
    }
}

spongestart{
    eula true
    minecraft "1.12.1"
    type "experimental"
    spongeForgeVersion properties.spongeforge
    forgeServerFolder "run/API7/"
}

dependencies {
    compile("org.spongepowered:spongeapi:${spongeapi}")
    compile("de.btobastian.javacord:javacord:2.0.15:shaded")
    compile("de.btobastian.sdcf4j:sdcf4j-core:1.0.5")
    compile("de.btobastian.sdcf4j:sdcf4j-javacord:1.0.5")
}

task fatJar(type: Jar) {
    baseName = project.name + "-all"
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

shadowJar {
    dependencies {
        include dependency("commons-logging:commons-logging")
        include dependency("org.apache.commons:commons-lang3")
        include dependency("org.apache.httpcomponents:httpcore")
        include dependency("org.apache.httpcomponents:httpclient")
        include dependency("de.btobastian.javacord:javacord")
        include dependency("de.btobastian.sdcf4j:sdcf4j-core")
        include dependency("de.btobastian.sdcf4j:sdcf4j-javacord")

        relocate "org.apache.http", "shaded.apache.http"
        relocate "org.apache.commons", "shaded.apache.commons"
    }
}

build.dependsOn(shadowJar)